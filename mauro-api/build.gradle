plugins {
    id("groovy")
    //id("com.gradleup.shadow") version "9.0.0"
    id("io.micronaut.application") version "4.6.1"
    id("io.micronaut.aot") version "4.6.1"
    id("codenarc")
    id("java-test-fixtures")
    id "com.vanniktech.maven.publish" version "0.34.0"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':mauro-domain')
    implementation project(':mauro-client')
    implementation project(':mauro-persistence')

    testImplementation testFixtures(project(':mauro-client'))          // Pull in the filter that overwrites the port
    testImplementation testFixtures(project(':mauro-persistence'))     // Pull in the containerized testing definition

    compileOnly("io.micronaut.openapi:micronaut-openapi") {
        //        exclude group: 'org.slf4j'
    }
    compileOnly("io.swagger.core.v3:swagger-annotations")
    compileOnly("io.micronaut.data:micronaut-data-processor")
    compileOnly("io.micronaut.validation:micronaut-validation-processor")
    implementation("io.micronaut.test:micronaut-test-core")
    implementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut:micronaut-jackson-databind")
    implementation("io.micronaut.data:micronaut-data-jdbc")
    implementation("io.micronaut.groovy:micronaut-runtime-groovy")
    implementation("io.micronaut.reactor:micronaut-reactor")
    implementation("io.micronaut.reactor:micronaut-reactor-http-client")
    implementation("jakarta.annotation:jakarta.annotation-api")
    compileOnly("io.micronaut:micronaut-http-validation")
    implementation("ch.qos.logback:logback-classic")
    implementation("ch.qos.logback:logback-core")
    implementation("io.micronaut.validation:micronaut-validation")
    implementation("io.micronaut:micronaut-management")
    compileOnly("io.micronaut:micronaut-inject-groovy")
    runtimeOnly("org.yaml:snakeyaml")
    implementation("io.micronaut.flyway:micronaut-flyway")
    runtimeOnly("org.postgresql:postgresql")
    runtimeOnly("org.flywaydb:flyway-database-postgresql")
    implementation("io.micronaut.cache:micronaut-cache-core")
    implementation("io.micronaut.cache:micronaut-cache-caffeine")


    implementation("io.micronaut.sql:micronaut-jdbc-hikari")
    implementation('org.apache.groovy:groovy-json:4.0.19')
    implementation('org.apache.groovy:groovy-xml:4.0.25')


    // Security
    implementation("io.micronaut.security:micronaut-security-session")
    compileOnly("io.micronaut.security:micronaut-security-annotations")
    // OpenID Connect
    implementation("io.micronaut.security:micronaut-security-oauth2")
    implementation("io.micronaut.security:micronaut-security-jwt")

    implementation('org.jboss.resteasy:resteasy-client:7.0.0.Alpha4')
    testImplementation('org.htmlunit:htmlunit:4.6.0')
    //Email
    implementation("io.micronaut.email:micronaut-email-javamail")
    runtimeOnly("org.eclipse.angus:angus-mail")

    testImplementation ("io.micronaut:micronaut-inject-groovy")

    testRuntimeOnly("org.testcontainers:testcontainers")
    testRuntimeOnly("org.testcontainers:postgresql")
    testRuntimeOnly("org.postgresql:postgresql")


    implementation('org.apache.commons:commons-rng-simple:1.5')
    implementation('org.apache.commons:commons-text:1.15.0')

    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    testImplementation "io.micronaut.test:micronaut-test-spock"
    testImplementation("org.spockframework:spock-core") {
        exclude group: "org.codehaus.groovy", module: "groovy-all"
    }

    testImplementation 'org.reflections:reflections:0.10.2'

    codenarc 'org.apache.groovy:groovy-all:4.0.24'
    codenarc 'org.codenarc:CodeNarc:3.6.0-groovy-4.0'

}


application {
    mainClass.set("org.maurodata.controller.Application")
}

//graalvmNative.toolchainDetection = false

micronaut {
    runtime("netty")
    testRuntime("spock2")
    processing {
        incremental(true)
        annotations("org.maurodata.*")
    }
    aot {
        // Please review carefully the optimizations enabled below
        // Check https://micronaut-projects.github.io/micronaut-aot/latest/guide/ for more details
        optimizeServiceLoading = false
        convertYamlToJava = false
        precomputeOperations = true
        cacheEnvironment = true
        optimizeClassLoading = true
        deduceEnvironment = true
        optimizeNetty = true
    }
}


codenarc {
    toolVersion = "3.6.0-groovy-4.0"
    configFile = rootProject.file("config/codenarc/codenarc.groovy")
}

codenarcMain {
    compilationClasspath = sourceSets.main.compileClasspath + sourceSets.main.output
}

codenarcTest {
    compilationClasspath = codenarcMain.compilationClasspath + sourceSets.test.compileClasspath + sourceSets.test.output
}

[codenarcMain, codenarcTest]*.ignoreFailures = true

docker {
    registryCredentials {
        username = System.getenv('DOCKER_USERNAME')
        password = System.getenv('DOCKER_PASSWORD')
    }
}

import groovy.json.JsonSlurper

def mauroRepository = 'https://mauro-repository.com'
def repositoryPath='artifacts-snapshots/mauroDataMapper/mdm-ui'

def uiVersion = project.findProperty('uiVersion')
def uiRepoApi  = "${mauroRepository}/api/maven/details/${repositoryPath}"
def uiTargetDir = project.layout.buildDirectory.dir("docker/main/layers/resources/public").get().asFile

def latestLts(product, fallback, detectLTS) {
    if(!detectLTS){return fallback}
    try {
        def endoflife = 'https://endoflife.date/api/v1/products/'
        def json = new JsonSlurper().parse(new URI("${endoflife}${product}").toURL())

        def releases = json.result.releases
        def ltsReleases = releases.findAll {r ->
            (r.isLts == true) || (r.label =~ /LTS/)
        }

        def latest = ltsReleases.sort {a, b ->
            b.releaseDate <=> a.releaseDate
        }[0]

        if (latest.codename == null) {
            return latest.name
        }

        return latest.codename.substring(0, latest.codename.indexOf(' ')).toLowerCase()
    }
    catch(Exception e){
        logger.warn("âš ï¸ Failed to fetch LTS info for ${product}, falling back to ${fallback} (reason: ${e.message})")
        return fallback
    }
}

def temurinDefault='21'
def ubuntuDefault='noble'
def detectLTS=false

def dockerTag="eclipse-temurin:${latestLts('eclipse-temurin',temurinDefault,detectLTS)}-jre-${latestLts('ubuntu',ubuntuDefault,detectLTS)}"

def dockerIncludeDatabase = !gradle.startParameter.taskNames.any { it.contains('dockerBuild') && (it.contains("NoDatabase") || it.contains("Backend")) }
def dockerIncludeUI = !gradle.startParameter.taskNames.any { it.contains('dockerBuild') && it.contains("Backend") }
def dockerBackendOnly = !dockerIncludeDatabase && !dockerIncludeUI

tasks.register('printDockerTag') {
    doLast {
        println "Would use Docker base image: ${dockerTag}"
    }
}

tasks.register('printItOut') {

    doLast {
        println "dockerIncludeDatabase = ${dockerIncludeDatabase}"
    }
}

tasks.register('fetchMDMUI') {
    doLast {
        if(!dockerIncludeUI) {

            if(uiTargetDir.exists()) {
                println "Removing UI files"
                project.delete(uiTargetDir)
            }

            return
        }

        println "Fetching UI list from $uiRepoApi"
        def jsonText = new URI(uiRepoApi).toURL().getText(requestProperties: ['Accept': 'application/json'])
        def parsed = new JsonSlurper().parseText(jsonText)

        def fileName
        if (uiVersion) {
            fileName = "mdm-ui-${uiVersion}.tgz"
            if (!parsed.files*.name.contains(fileName)) {
                throw new GradleException("UI version ${uiVersion} not found in repository")
            }
        } else {
            fileName = parsed.files.last().name
        }

        def downloadUrl = "${mauroRepository}/${repositoryPath}/${fileName}"
        println "Downloading UI: $fileName from ${downloadUrl}"
        uiTargetDir.mkdirs()

        def tmpFile = File.createTempFile('mdmui', '.tgz')
        tmpFile.withOutputStream { out ->
            out << new URI(downloadUrl).toURL().openStream()
        }

        println "Extracting UI ${tmpFile.absolutePath} into ${uiTargetDir.absolutePath}"

        def processTar = ['tar', '-xzf', tmpFile.absolutePath, '-C', uiTargetDir.absolutePath, '--strip-components=1'].execute()
        processTar.waitFor()

        tmpFile.delete()

        println "Patching Angular API endpoint to relative /api"
        def jsFiles = fileTree(dir: uiTargetDir, include: 'main.*.js').files

        if (jsFiles) {

            def sedInlineArgs = System.getProperty("os.name").toLowerCase().contains("mac") ?
                                ['-i', ''] : ['-i']

            def jsFilePaths = jsFiles.collect { it.absolutePath }

            def cmd = ['sed'] +
                      sedInlineArgs +
                      ['s|this.apiEndpoint="http://localhost:8080/api"|this.apiEndpoint="/api"|g'] +
                      jsFilePaths

            def processSed = new ProcessBuilder(cmd)
            .directory(uiTargetDir)
            .start()
            processSed.waitFor()

        } else {
            logger.warn("No main.*.js files found in ${uiTargetDir}")
        }
    }
}

tasks.named('dockerBuild') {
    dependsOn(tasks.named('fetchMDMUI'))
    if(!dockerIncludeDatabase) {
        images = ["maurodatamapper/mauro:${System.getProperty('os.arch')}-nodb-${version}"]
    } else {
        images = ["maurodatamapper/mauro:${System.getProperty('os.arch')}-${version}"]
    }
}

tasks.register('stageDockerFiles', Copy) {
    def dockerMainScripts=project.layout.buildDirectory.dir("docker/main/scripts").get().asFile
    if(dockerMainScripts.exists()){
        delete(dockerMainScripts)
    }
    mkdir(dockerMainScripts)
    if(!dockerIncludeDatabase) {
        from file('docker/noDB')
        into dockerMainScripts
    } else {
        from file('docker/all')
        into dockerMainScripts
    }
}

tasks.named('dockerBuild') {
    dependsOn 'stageDockerFiles'
}

tasks.named("dockerfile") {
    baseImage("${dockerTag}")

    if(!dockerIncludeDatabase) {
        instruction """
ENV MICRONAUT_ENVIRONMENTS='docker,datasources,javamail,mauro'

ENV INIT_DIR=/opt/init
ENV LOGS_DIRECTORY=/var/logs

RUN set -eux; \
    arch="\$(dpkg --print-architecture)"; \
    case "\${arch}" in \
        amd64) \
            MIRROR_HOST="archive.ubuntu.com"; \
            MIRROR_PATH="ubuntu/"; \
            ;; \
        arm64) \
            MIRROR_HOST="ports.ubuntu.com"; \
            MIRROR_PATH="ubuntu-ports/"; \
            ;; \
        *) \
            exit 1 \
            ;; \
    esac; \
    cp -p "/etc/apt/sources.list.d/ubuntu.sources" "/etc/apt/sources.list.d/ubuntu-uk.sources" && sed -i -E "s|^URIs:.*|URIs: http://uk.\${MIRROR_HOST}/\${MIRROR_PATH}|g" "/etc/apt/sources.list.d/ubuntu-uk.sources"; \
    cp -p "/etc/apt/sources.list.d/ubuntu.sources" "/etc/apt/sources.list.d/ubuntu-ie.sources" && sed -i -E "s|^URIs:.*|URIs: http://ie.\${MIRROR_HOST}/\${MIRROR_PATH}|g" "/etc/apt/sources.list.d/ubuntu-ie.sources"
RUN apt-get update && apt-get dist-upgrade -y && apt-get install -y --no-install-recommends unzip iproute2 && apt-get clean && rm -rf /var/lib/apt/lists/*
RUN mkdir -p \${LOGS_DIRECTORY}
RUN mkdir -p \${INIT_DIR}
VOLUME ["\${INIT_DIR}"]

COPY scripts/micronaut/* /usr/local/bin/
COPY scripts/startup/* /usr/local/bin/
RUN chmod +x /usr/local/bin/*

ENTRYPOINT ["docker-startup.sh"]
    """
    } else {

        instruction """
ENV MICRONAUT_ENVIRONMENTS='docker,datasources,javamail,mauro'
ENV DATABASE_NAME=sandbox
ENV DATABASE_USERNAME=sandbox
ENV DATABASE_PASSWORD=sandbox

ENV INIT_DIR=/opt/init
ENV DATABASE_DIRECTORY=/var/lib/postgresql/data
ENV LOGS_DIRECTORY=/var/logs
ENV DATABASE_LOGS_DIRECTORY=\${LOGS_DIRECTORY}/postgres
ENV DATABASE_SNAPSHOTS_DIRECTORY=/database

RUN set -eux; \
    arch="\$(dpkg --print-architecture)"; \
    case "\${arch}" in \
        amd64) \
            MIRROR_HOST="archive.ubuntu.com"; \
            MIRROR_PATH="ubuntu/"; \
            ;; \
        arm64) \
            MIRROR_HOST="ports.ubuntu.com"; \
            MIRROR_PATH="ubuntu-ports/"; \
            ;; \
        *) \
            exit 1 \
            ;; \
    esac; \
    cp -p "/etc/apt/sources.list.d/ubuntu.sources" "/etc/apt/sources.list.d/ubuntu-uk.sources" && sed -i -E "s|^URIs:.*|URIs: http://uk.\${MIRROR_HOST}/\${MIRROR_PATH}|g" "/etc/apt/sources.list.d/ubuntu-uk.sources"; \
    cp -p "/etc/apt/sources.list.d/ubuntu.sources" "/etc/apt/sources.list.d/ubuntu-ie.sources" && sed -i -E "s|^URIs:.*|URIs: http://ie.\${MIRROR_HOST}/\${MIRROR_PATH}|g" "/etc/apt/sources.list.d/ubuntu-ie.sources"
RUN apt-get update && apt-get dist-upgrade -y && apt-get install -y --no-install-recommends postgresql postgresql-contrib unzip iproute2 && apt-get clean && rm -rf /var/lib/apt/lists/*
RUN mkdir -p \${DATABASE_DIRECTORY} && chown -R postgres:postgres /var/lib/postgresql
RUN mkdir -p \${LOGS_DIRECTORY} && mkdir -p \${DATABASE_LOGS_DIRECTORY} && chown -R postgres:postgres \${DATABASE_LOGS_DIRECTORY}
VOLUME ["\${DATABASE_DIRECTORY}","\${DATABASE_LOGS_DIRECTORY}"]
RUN mkdir -p \${DATABASE_SNAPSHOTS_DIRECTORY} && chown -R postgres:postgres \${DATABASE_SNAPSHOTS_DIRECTORY}
VOLUME ["\${DATABASE_SNAPSHOTS_DIRECTORY}"]
RUN mkdir -p \${INIT_DIR}
VOLUME ["\${INIT_DIR}"]

COPY scripts/postgres/* /usr/local/bin/
COPY scripts/micronaut/* /usr/local/bin/
COPY scripts/startup/* /usr/local/bin/
RUN chmod +x /usr/local/bin/*

EXPOSE 5432
ENTRYPOINT ["docker-startup.sh"]
    """
    }
}

tasks.named("dockerBuild") {
    dependsOn("injectEnvIntoDockerfile")
}

tasks.register("injectEnvIntoDockerfile") {
    doLast {
        def dockerfile = file("build/docker/main/Dockerfile")
        def contents = dockerfile.text
        int entrypointPos = contents.lastIndexOf('ENTRYPOINT')
        dockerfile.text = dockerfile.text.substring(0,entrypointPos)
    }
}

tasks.register('dockerBuildMultiArch') {
    dependsOn(tasks.named('buildLayers'))
    dependsOn(tasks.named('dockerfile'))
    dependsOn(tasks.named('fetchMDMUI'))
    dependsOn(tasks.named('injectEnvIntoDockerfile'))
    dependsOn(tasks.named('stageDockerFiles'))

    doLast {
        println "ðŸ³ Running docker build with multi-arch buildx command..."

        def imageName
        if(dockerBackendOnly) {
            imageName = "maurodatamapper/mauro:backend-${version}"
        }
        else {
            if (!dockerIncludeDatabase) {
                imageName = "maurodatamapper/mauro:nodb-${version}"
            } else {
                imageName = "maurodatamapper/mauro:${version}"
            }
        }
        def location = project.layout.buildDirectory.dir("docker/main").get().asFile.absolutePath

        def processBuilderDockerx = new ProcessBuilder('docker', 'buildx', 'build', '--platform', 'linux/amd64,linux/arm64', '--tag', "${imageName}", "${location}")
        processBuilderDockerx.inheritIO()
        def processDockerx = processBuilderDockerx.start()
        def rc = processDockerx.waitFor()
        if (rc != 0) throw new GradleException("buildx failed (${rc})")
    }
}

tasks.register('dockerBuildNoDatabase') {
    dependsOn(tasks.named('dockerBuild'))
}

tasks.register('dockerBuildMultiArchNoDatabase') {
    dependsOn(tasks.named('dockerBuildMultiArch'))
}

tasks.register('dockerBuildBackend') {
    dependsOn(tasks.named('dockerBuildMultiArch'))
}
