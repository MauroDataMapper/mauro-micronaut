plugins {
    id("groovy")
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("io.micronaut.application") version "4.4.5"
    id("io.micronaut.aot") version "4.4.5"
    id("maven-publish")
    id("codenarc")
    id("java-test-fixtures")
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly("io.micronaut.openapi:micronaut-openapi") {
        //        exclude group: 'org.slf4j'
    }
    compileOnly("io.swagger.core.v3:swagger-annotations")
    compileOnly("io.micronaut.data:micronaut-data-processor")
    compileOnly("io.micronaut.validation:micronaut-validation-processor")
    implementation("io.micronaut.test:micronaut-test-core")
    implementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut:micronaut-jackson-databind")
    implementation("io.micronaut.data:micronaut-data-jdbc")
    implementation("io.micronaut.groovy:micronaut-runtime-groovy")
    implementation("io.micronaut.reactor:micronaut-reactor")
    implementation("io.micronaut.reactor:micronaut-reactor-http-client")
    implementation("jakarta.annotation:jakarta.annotation-api")
    compileOnly("io.micronaut:micronaut-http-validation")
    implementation("ch.qos.logback:logback-classic")
    implementation("ch.qos.logback:logback-core")
    implementation("io.micronaut.validation:micronaut-validation")
    implementation("io.micronaut:micronaut-management")
    compileOnly("io.micronaut:micronaut-inject-groovy")
    runtimeOnly("org.yaml:snakeyaml")
    implementation("io.micronaut.flyway:micronaut-flyway")
    runtimeOnly("org.postgresql:postgresql")
    runtimeOnly("org.flywaydb:flyway-database-postgresql")

    implementation("io.micronaut.sql:micronaut-jdbc-hikari")
    implementation group: 'org.apache.groovy', name: 'groovy-json', version: '4.0.19'
    implementation group: 'org.apache.groovy', name: 'groovy-xml', version: '4.0.25'


    // Security
    implementation("io.micronaut.security:micronaut-security-session")
    compileOnly("io.micronaut.security:micronaut-security-annotations")
    // OpenID Connect
    implementation("io.micronaut.security:micronaut-security-oauth2")
    implementation("io.micronaut.security:micronaut-security-jwt")

    implementation group: 'org.jboss.resteasy', name: 'resteasy-client', version: '7.0.0.Alpha4'
    testImplementation group: 'org.htmlunit', name: 'htmlunit', version: '4.6.0'
    //Email
    implementation("io.micronaut.email:micronaut-email-javamail")
    runtimeOnly("org.eclipse.angus:angus-mail")

    testImplementation ("io.micronaut:micronaut-inject-groovy")

    testRuntimeOnly("org.testcontainers:testcontainers")
    testRuntimeOnly("org.testcontainers:postgresql")
    testRuntimeOnly("org.postgresql:postgresql")


    implementation group: 'org.apache.commons', name: 'commons-rng-simple', version: '1.5'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.10.0'

    testRuntimeOnly("org.testcontainers:testcontainers")
    testRuntimeOnly("org.testcontainers:postgresql")

    testImplementation "io.micronaut.test:micronaut-test-spock"
    testImplementation("org.spockframework:spock-core") {
        exclude group: "org.codehaus.groovy", module: "groovy-all"
    }

    testImplementation 'org.reflections:reflections:0.10.2'
}


application {
    mainClass.set("org.maurodata.controller.Application")
}
java {
    sourceCompatibility = JavaVersion.toVersion("17")
    targetCompatibility = JavaVersion.toVersion("17")
}

graalvmNative.toolchainDetection = false
micronaut {
    runtime("netty")
    testRuntime("spock2")
    processing {
        incremental(true)
        annotations("org.maurodata.*")
    }
    aot {
        // Please review carefully the optimizations enabled below
        // Check https://micronaut-projects.github.io/micronaut-aot/latest/guide/ for more details
        optimizeServiceLoading = false
        convertYamlToJava = false
        precomputeOperations = true
        cacheEnvironment = true
        optimizeClassLoading = true
        deduceEnvironment = true
        optimizeNetty = true
    }
}

publishing {
    repositories {
        maven {
            name = "MauroSnapshotRepository"
            url = "https://mauro-repository.com/libs-snapshot-local"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.maurodata'
            artifactId = 'mauro-api'
            version = "0.0.1-SNAPSHOT"
            from components.java
        }
    }
}

dependencies {
    implementation project(':mauro-domain')
    implementation project(':mauro-client')
    implementation project(':mauro-persistence')

    testImplementation testFixtures(project(':mauro-client'))          // Pull in the filter that overwrites the port
    testImplementation testFixtures(project(':mauro-persistence'))     // Pull in the containerized testing definition
}

codenarc {
    toolVersion = "3.3.0-groovy-4.0"
    configFile = rootProject.file("config/codenarc/codenarc.groovy")
}

codenarcMain {
    compilationClasspath = sourceSets.main.compileClasspath + sourceSets.main.output
}

codenarcTest {
    compilationClasspath = codenarcMain.compilationClasspath + sourceSets.test.compileClasspath + sourceSets.test.output
}

[codenarcMain, codenarcTest]*.ignoreFailures = true

import groovy.json.JsonSlurper

def mauroRepository = 'https://mauro-repository.com'
def repositoryPath='artifacts-snapshots/mauroDataMapper/mdm-ui'

def uiVersion = project.findProperty('uiVersion')
def uiRepoApi  = "${mauroRepository}/api/maven/details/${repositoryPath}"
def uiTargetDir = project.layout.buildDirectory.dir("docker/main/layers/resources/public").get().asFile

def latestLts(product, fallback, detectLTS) {
    if(!detectLTS){return fallback}
    try {
        def endoflife = 'https://endoflife.date/api/v1/products/'
        def json = new JsonSlurper().parse(new URI("${endoflife}${product}").toURL())

        def releases = json.result.releases
        def ltsReleases = releases.findAll {r ->
            (r.isLts == true) || (r.label =~ /LTS/)
        }

        def latest = ltsReleases.sort {a, b ->
            b.releaseDate <=> a.releaseDate
        }[0]

        if (latest.codename == null) {
            return latest.name
        }

        return latest.codename.substring(0, latest.codename.indexOf(' ')).toLowerCase()
    }
    catch(Exception e){
        logger.warn("⚠️ Failed to fetch LTS info for ${product}, falling back to ${fallback} (reason: ${e.message})")
        return fallback
    }
}

def temurinDefault='21'
def ubuntuDefault='noble'
def detectLTS=false

def dockerTag="eclipse-temurin:${latestLts('eclipse-temurin',temurinDefault,detectLTS)}-jre-${latestLts('ubuntu',ubuntuDefault,detectLTS)}"

tasks.register('printDockerTag') {
    doLast {
        println "Would use Docker base image: ${dockerTag}"
    }
}

tasks.register('fetchMDMUI') {
    doLast {
        println "Fetching UI list from $uiRepoApi"
        def jsonText = new URI(uiRepoApi).toURL().getText(requestProperties: ['Accept': 'application/json'])
        def parsed = new JsonSlurper().parseText(jsonText)

        def fileName
        if (uiVersion) {
            fileName = "mdm-ui-${uiVersion}.tgz"
            if (!parsed.files*.name.contains(fileName)) {
                throw new GradleException("UI version ${uiVersion} not found in repository")
            }
        } else {
            fileName = parsed.files.last().name
        }

        def downloadUrl = "${mauroRepository}/${repositoryPath}/${fileName}"
        println "Downloading UI: $fileName"
        uiTargetDir.mkdirs()

        def tmpFile = File.createTempFile('mdmui', '.tgz')
        tmpFile.withOutputStream { out ->
            out << new URI(downloadUrl).toURL().openStream()
        }

        println "Extracting UI into ${uiTargetDir.absolutePath}"
        project.exec {
            commandLine 'tar', '-xzf', tmpFile.absolutePath, '-C', uiTargetDir.absolutePath, '--strip-components=1'
        }
        tmpFile.delete()

        println "Patching Angular API endpoint to relative /api"
        def jsFiles = fileTree(dir: uiTargetDir, include: 'main.*.js').files

        if (jsFiles) {
            project.exec {
                workingDir uiTargetDir

                // macOS BSD sed needs an empty string after -i, GNU sed does not
                def sedInlineArgs = System.getProperty("os.name").toLowerCase().contains("mac") ?
                                    ['-i', ''] : ['-i']

                commandLine 'sed', *sedInlineArgs, 's|this.apiEndpoint="http://localhost:8080/api"|this.apiEndpoint="/api"|g', *jsFiles*.absolutePath
            }
        } else {
            logger.warn("No main.*.js files found in ${uiTargetDir}")
        }
    }
}

tasks.named('dockerBuild') {
    dependsOn(tasks.named('fetchMDMUI'))
}

tasks.register('stageDockerFiles', Copy) {
    from file('docker')
    into project.layout.buildDirectory.dir("docker/main").get().asFile
}

tasks.named('dockerBuild') {
    dependsOn 'stageDockerFiles'
}

tasks.named("dockerfile") {
    baseImage("${dockerTag}")

    instruction """
ENV MICRONAUT_ENVIRONMENTS=docker
ENV DATABASE_NAME=sandbox
ENV DATABASE_USERNAME=sandbox
ENV DATABASE_PASSWORD=sandbox

ENV INIT_DIR=/opt/init
ENV DATABASE_DIRECTORY=/var/lib/postgresql/data
ENV LOGS_DIRECTORY=/var/logs
ENV DATABASE_LOGS_DIRECTORY=\${LOGS_DIRECTORY}/postgres
ENV DATABASE_SNAPSHOTS_DIRECTORY=/database

RUN apt-get update && apt-get install -y --no-install-recommends postgresql postgresql-contrib unzip iproute2 && rm -rf /var/lib/apt/lists/*
RUN mkdir -p \${DATABASE_DIRECTORY} && chown -R postgres:postgres /var/lib/postgresql
RUN mkdir -p \${LOGS_DIRECTORY} && mkdir -p \${DATABASE_LOGS_DIRECTORY} && chown -R postgres:postgres \${DATABASE_LOGS_DIRECTORY}
VOLUME ["\${DATABASE_DIRECTORY}","\${DATABASE_LOGS_DIRECTORY}"]
RUN mkdir -p \${DATABASE_SNAPSHOTS_DIRECTORY} && chown -R postgres:postgres \${DATABASE_SNAPSHOTS_DIRECTORY}
VOLUME ["\${DATABASE_SNAPSHOTS_DIRECTORY}"]
RUN mkdir -p \${INIT_DIR}
VOLUME ["\${INIT_DIR}"]

COPY postgres/* /usr/local/bin/
COPY micronaut/* /usr/local/bin/
COPY startup/* /usr/local/bin/
RUN chmod +x /usr/local/bin/*

EXPOSE 5432
ENTRYPOINT ["docker-startup.sh"]
    """
}

tasks.named("dockerBuild") {
    dependsOn("injectEnvIntoDockerfile")
}

tasks.register("injectEnvIntoDockerfile") {
    doLast {
        def dockerfile = file("build/docker/main/Dockerfile")
        def contents = dockerfile.text
        int entrypointPos = contents.lastIndexOf('ENTRYPOINT')
        dockerfile.text = dockerfile.text.substring(0,entrypointPos)
    }
}
