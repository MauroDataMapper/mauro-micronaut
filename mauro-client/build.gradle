plugins {
    id 'groovy'
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("java-library")
    id("java-test-fixtures")
    id("io.micronaut.library") version "4.4.5"
    id("io.micronaut.aot") version "4.4.5"
    id("maven-publish")
    id("codenarc")
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':mauro-domain')

    annotationProcessor "io.micronaut:micronaut-inject-java"
    annotationProcessor("io.micronaut:micronaut-inject-groovy")
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation("io.micronaut.test:micronaut-test-spock")
    implementation("io.micronaut.openapi:micronaut-openapi")
    implementation("io.micronaut:micronaut-jackson-databind")
    implementation 'io.micronaut:micronaut-http-client'
    implementation 'io.micronaut:micronaut-http-server'
    implementation "io.micronaut:micronaut-runtime"
    implementation "io.micronaut:micronaut-inject"
    implementation("io.micronaut.picocli:micronaut-picocli")
    implementation("io.micronaut.security:micronaut-security")
    annotationProcessor("info.picocli:picocli-codegen")


    testFixturesImplementation("io.micronaut.test:micronaut-test-spock")
    testFixturesImplementation("io.micronaut.sql:micronaut-jdbc-hikari")
    testFixturesImplementation('io.micronaut:micronaut-http-server')
    testFixturesImplementation("org.junit.jupiter:junit-jupiter-api:5.2.0")
    testFixturesImplementation("org.junit.jupiter:junit-jupiter-engine:5.2.0")

}

// This is necessary to ensure the TestApiClientFilter is turned into a bean and injectable
micronaut {
    processing {
        sourceSets (sourceSets.testFixtures)
    }
}

jar {
    exclude("**/logback.xml")
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "MauroSnapshotRepository"
            url = "https://mauro-repository.com/libs-snapshot-local"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.maurodata'
            artifactId = 'mauro-client'
            version = "0.0.2-SNAPSHOT"
            from components.java
        }
    }
}



codenarc {
    toolVersion = "3.3.0-groovy-4.0"
    configFile = rootProject.file("config/codenarc/codenarc.groovy")
}

codenarcMain {
    compilationClasspath = sourceSets.main.compileClasspath + sourceSets.main.output
}

codenarcTest {
    compilationClasspath = codenarcMain.compilationClasspath + sourceSets.test.compileClasspath + sourceSets.test.output
}

[codenarcMain, codenarcTest, codenarcTestFixtures]*.ignoreFailures = true