plugins {
    id("groovy")
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("io.micronaut.library") version "4.4.5"
    id("maven-publish")
    id("codenarc")
    id("java-library")
    id("java-test-fixtures")
}


repositories {
    mavenCentral()
}

dependencies {
    implementation("io.micronaut.groovy:micronaut-runtime-groovy")
    implementation("jakarta.annotation:jakarta.annotation-api")
    implementation("io.micronaut:micronaut-management")
    compileOnly("io.micronaut:micronaut-inject-groovy")

    runtimeOnly("org.postgresql:postgresql")
    runtimeOnly("org.flywaydb:flyway-database-postgresql")

    implementation("io.micronaut.flyway:micronaut-flyway")
    implementation("org.flywaydb:flyway-core")
    compileOnly("io.micronaut.data:micronaut-data-processor")
    compileOnly("io.micronaut.validation:micronaut-validation-processor")
    implementation("io.micronaut:micronaut-jackson-databind")
    implementation("io.micronaut.data:micronaut-data-jdbc")
    implementation("io.micronaut.groovy:micronaut-runtime-groovy")
    runtimeOnly("ch.qos.logback:logback-classic")
    implementation("io.micronaut.validation:micronaut-validation")
    implementation("io.micronaut.cache:micronaut-cache-core")
//    implementation("io.micronaut.cache:micronaut-cache-ehcache")
    implementation("io.micronaut.cache:micronaut-cache-caffeine")
    implementation("io.micronaut.cache:micronaut-cache-management")
    runtimeOnly("org.yaml:snakeyaml")

    testImplementation("io.micronaut.test:micronaut-test-spock")
    testRuntimeOnly("org.testcontainers:testcontainers")
    testRuntimeOnly("org.testcontainers:postgresql")
    testRuntimeOnly("org.postgresql:postgresql")

    testFixturesImplementation("io.micronaut.test:micronaut-test-spock")
    testFixturesImplementation("io.micronaut.sql:micronaut-jdbc-hikari")
    testFixturesImplementation("org.junit.jupiter:junit-jupiter-api:5.2.0")
    testFixturesImplementation("org.junit.jupiter:junit-jupiter-engine:5.2.0")

    // To find flyway migrations in jars
    implementation 'io.github.classgraph:classgraph:4.8.158'
}

java {
    sourceCompatibility = JavaVersion.toVersion("17")
    targetCompatibility = JavaVersion.toVersion("17")
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "MauroSnapshotRepository"
            url = "https://mauro-repository.com/libs-snapshot-local"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.maurodata'
            artifactId = 'mauro-persistence'
            version = "0.0.2-SNAPSHOT"
            from components.java
        }
    }
}

dependencies {
    implementation project(':mauro-domain')
}

codenarc {
    toolVersion = "3.3.0-groovy-4.0"
    configFile = rootProject.file("config/codenarc/codenarc.groovy")
}

codenarcMain {
    compilationClasspath = sourceSets.main.compileClasspath + sourceSets.main.output
}

codenarcTest {
    compilationClasspath = codenarcMain.compilationClasspath + sourceSets.test.compileClasspath + sourceSets.test.output
}

[codenarcMain, codenarcTest, codenarcTestFixtures]*.ignoreFailures = true

jar {
    exclude("**/logback.xml")
}
