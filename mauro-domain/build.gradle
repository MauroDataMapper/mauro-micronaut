plugins {
    id("groovy")
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("maven-publish")
    id("io.micronaut.library") version "4.1.2"
    id("codenarc")
}

version = "0.1"
group = "uk.ac.ox.softeng.mauro"

repositories {
    mavenCentral()
}

dependencies {
    runtimeOnly("ch.qos.logback:logback-classic")
    compileOnly("io.micronaut.data:micronaut-data-processor")
    implementation("io.micronaut.validation:micronaut-validation")
    implementation("io.micronaut:micronaut-jackson-databind")

    testImplementation "io.micronaut.test:micronaut-test-spock"

}

java {
    sourceCompatibility = JavaVersion.toVersion("17")
    targetCompatibility = JavaVersion.toVersion("17")
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'uk.ac.ox.softeng.mauro'
            artifactId = 'mauro-domain'
            version = "0.0.1-SNAPSHOT"
            from components.java
        }
    }
}

test {
    useJUnitPlatform()
}

codenarc {
    toolVersion = "3.3.0-groovy-4.0"
    configFile = rootProject.file("config/codenarc/codenarc.groovy")
}

codenarcMain {
    compilationClasspath = sourceSets.main.compileClasspath + sourceSets.main.output
}

codenarcTest {
    compilationClasspath = codenarcMain.compilationClasspath + sourceSets.test.compileClasspath + sourceSets.test.output
}

[codenarcMain, codenarcTest]*.ignoreFailures = true
