plugins {
    id("groovy")
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("maven-publish")
    id("io.micronaut.library") version "4.4.5"
    id("codenarc")
}

repositories {
    mavenCentral()
}

dependencies {
    runtimeOnly("ch.qos.logback:logback-classic")
    implementation("io.micronaut.data:micronaut-data-processor")
    implementation("io.micronaut.validation:micronaut-validation")
    implementation("io.micronaut:micronaut-jackson-databind")

    // camel case
    implementation("com.google.guava:guava:33.4.8-jre")

    // For parsing java dates / instants
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310")

    // For importer parameters introspection
    implementation('org.apache.commons:commons-lang3:3.17.0')

    implementation('org.apache.groovy:groovy-json:4.0.19')
    testImplementation "io.micronaut.test:micronaut-test-spock"
}

java {
    sourceCompatibility = JavaVersion.toVersion("17")
    targetCompatibility = JavaVersion.toVersion("17")
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        // The target repository
        maven {
            // The url of the repository, where the artifacts will be published
            url = "https://maven.pkg.github.com/MauroDataMapper/mauro-package-repository"
            credentials {
                // The credentials (described in the next section)
                username = project.findProperty("gpr.user")
                password = project.findProperty("gpr.key")
            }
        }
        maven {
            name = "MauroSnapshotRepository"
            url = "https://mauro-repository.com/libs-snapshot-local"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            groupId = 'org.maurodata'
            artifactId = 'mauro-domain'
            version = "0.0.1-SNAPSHOT"
            from components.java
        }
    }
}

test {
    useJUnitPlatform()
}

codenarc {
    toolVersion = "3.3.0-groovy-4.0"
    configFile = rootProject.file("config/codenarc/codenarc.groovy")
}

codenarcMain {
    compilationClasspath = sourceSets.main.compileClasspath + sourceSets.main.output
}

codenarcTest {
    compilationClasspath = codenarcMain.compilationClasspath + sourceSets.test.compileClasspath + sourceSets.test.output
}

[codenarcMain, codenarcTest]*.ignoreFailures = true
